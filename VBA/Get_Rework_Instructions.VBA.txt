Public ws As Worksheet
Public regx As Object
Public Const nodeID As String = "DMRList_ctl00_ReworkInstructionTableCell"

Sub Init()
    Dim dataRange As String
    Set regx = CreateObject("VBScript.RegExp")
    regx.Global = True
    regx.IgnoreCase = True
    regx.Pattern = "([A-Z]+)[0-9]+:\1[0-9]+"
    Set ws = Application.ActiveWorkbook.ActiveSheet
    dataRange = ""
    While Not regx.Test(dataRange)
        dataRange = InputBox("Enter the range with the data. MUST be in single column!")
    Wend
    Application.ScreenUpdating = False
    Main ws.Range(dataRange)
End Sub

Sub Main(ByRef dataRange As Range)
    Dim data() As Variant, i As Integer, offsetCols As Integer, ncrNum As String, dataCount As Integer, doc As Object
    data = dataRange.Value2
    dataCount = UBound(data)
    regx.Pattern = "\s*[\r\n]\s*"
    For i = LBound(data) To UBound(data)
        ncrNum = data(i, 1)
        Set doc = DOM("http://houston/ErpWeb/NCRStatus.aspx?NCRNumber=" & ncrNum)
        data(i, 1) = regx.Replace(doc.getElementById(nodeID).innerText, vbNewLine)
        Debug.Print "Scraping Page " & i & " of " & dataCount
    Next i
    Set doc = Nothing
    offsetCols = ws.UsedRange.Columns.Count - dataRange.Column + 1
    dataRange.Offset(0, offsetCols).Value2 = data
    dataRange.Offset(0, offsetCols).WrapText = False
    Application.ScreenUpdating = True
    MsgBox "Data Scraped Successfully!"
End Sub

Function DOM(ByRef url As String)
    Dim http As Object, html As Object, tries As Integer
    Set http = CreateObject("MSXML2.XMLHTTP.6.0")
    For tries = 1 To 5
        http.Open "GET", url, False
        http.send
        If http.Status = 200 Then
            Set html = CreateObject("htmlfile")
            html.body.innerHTML = http.responseText
            Set DOM = html
            Exit For
        End If
    Next tries
    Set http = Nothing
    Set html = Nothing
End Function
